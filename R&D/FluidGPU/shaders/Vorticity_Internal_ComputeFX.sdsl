//----------------------------------------------------------------------------------
// File:   FluidSim.fx
// Author: Sarah Tariq and Ignacio Llamas
// Email:  sdkfeedback@nvidia.com
// 
// Copyright (c) 2007 NVIDIA Corporation. All rights reserved.
//
// TO  THE MAXIMUM  EXTENT PERMITTED  BY APPLICABLE  LAW, THIS SOFTWARE  IS PROVIDED
// *AS IS*  AND NVIDIA AND  ITS SUPPLIERS DISCLAIM  ALL WARRANTIES,  EITHER  EXPRESS
// OR IMPLIED, INCLUDING, BUT NOT LIMITED  TO, IMPLIED WARRANTIES OF MERCHANTABILITY
// AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO EVENT SHALL  NVIDIA OR ITS SUPPLIERS
// BE  LIABLE  FOR  ANY  SPECIAL,  INCIDENTAL,  INDIRECT,  OR  CONSEQUENTIAL DAMAGES
// WHATSOEVER (INCLUDING, WITHOUT LIMITATION,  DAMAGES FOR LOSS OF BUSINESS PROFITS,
// BUSINESS INTERRUPTION, LOSS OF BUSINESS INFORMATION, OR ANY OTHER PECUNIARY LOSS)
// ARISING OUT OF THE  USE OF OR INABILITY  TO USE THIS SOFTWARE, EVEN IF NVIDIA HAS
// BEEN ADVISED OF THE POSSIBILITY OF SUCH DAMAGES.
//
//
//----------------------------------------------------------------------------------

//--------------------------------------------------------------------------------------
// Shaders to implement a "stable fluids" style semi-Lagrangian solver for 3D smoke
//--------------------------------------------------------------------------------------
// It assumes the velocity and pressure grids are collocated
// It handles boundary conditions for static obstacles stored as an in/out voxel volume
// BFECC is supported for smoke density advection
// The diffusion step is skipped
//--------------------------------------------------------------------------------------

shader Vorticity_Internal_ComputeFX : ComputeShaderBase, FluidGlobals
{
    //--------------------------------------------------------------------------------------
    // Textures
    //--------------------------------------------------------------------------------------
    
    Texture3D<float3> Texture_Velocity;
    RWTexture3D<float3> Texture_Vorticity; //Output Texture

    //--------------------------------------------------------------------------------------
    // Compute Shader
    //--------------------------------------------------------------------------------------
    
    override void Compute()
    {
        uint3 i = streams.DispatchThreadId;
        uint3 dim = FluidDimensions;

        uint3 LEFTCELL = uint3(max(i.x - 1, 0), i.y, i.z);
        uint3 RIGHTCELL = uint3(min(i.x + 1, dim.x - 1), i.y, i.z);
        uint3 BOTTOMCELL = uint3(i.x, max(i.y - 1, 0), i.z);
        uint3 TOPCELL = uint3(i.x, min(i.y + 1, dim.y - 1), i.z);
        uint3 DOWNCELL = uint3(i.x, i.y, max(i.z - 1, 0));
        uint3 UPCELL = uint3(i.x, i.y, min(i.z + 1, dim.z - 1));
    
        float3 L = Texture_Velocity[LEFTCELL];
        float3 R = Texture_Velocity[RIGHTCELL];
        float3 B = Texture_Velocity[BOTTOMCELL];
        float3 T = Texture_Velocity[TOPCELL];
        float3 D = Texture_Velocity[DOWNCELL];
        float3 U = Texture_Velocity[UPCELL];

        float3 vorticity;
        vorticity.xyz = 0.5 * float3( (( T.z - B.z ) - ( U.y - D.y )) ,
                                     (( U.x - D.x ) - ( R.z - L.z )) ,
                                     (( R.y - L.y ) - ( T.x - B.x )) );

        Texture_Vorticity[i] = vorticity.xyz;
    }
};