shader Simulation_ComputeFX : ComputeShaderBase, Global, ParticleStructPos4Vel4
{   

   // SETTINGS ---------------------------------------------------------------------
    cbuffer PerFrame
    {   
        uint ParticlesCount;
        bool Reset;
        float3 TargetPos;
        float TargetStrength;

    };


    // RESOURCES ---------------------------------------------------------------------

    RWStructuredBuffer<ParticlePos4Vel4> ParticlesBuffer;
    Buffer<float4> RandomValues;

    // =====================================================================
    // CS ==================================================================
    // =====================================================================

    override void Compute()
    {
        uint id = streams.DispatchThreadId.x;

        ParticlePos4Vel4 POut = ParticlesBuffer[id];

        float4 Rnd = RandomValues[id];

        // RESET ----------------------------------------------------------------------------
        if (Reset)
        {
            POut = (ParticlePos4Vel4) 0;
            POut.Pos.xyz = Rnd.xyz;
            POut.Vel = 0;
        }

         
        // ====================================================================================
        // PARTICLE DATA ======================================================================
        // ====================================================================================
        float3 Pos = POut.Pos.xyz;
        float3 Vel = POut.Vel.xyz;
        float VelLength = length(Vel);


        // DRAG:
        float drag = Rnd.w;
        if (VelLength > 0)
            Vel -= Vel * (1-drag);


        // ====================================================================================
        // FORCES =============================================================================
        // ====================================================================================

        float3 Force = 0;


        // Target
        Force += ((TargetPos + Rnd.xyz) - Pos) * TargetStrength;

        Vel += Force;

        // ====================================================================================
        // INTEGRATION ========================================================================
        // ====================================================================================

        // Vel 
        POut.Vel.xyz = Vel;
        // Pos
        Pos = Pos + Vel * TimeStep;
        POut.Pos.xyz = Pos;
    
        ParticlesBuffer[id] =  POut;
    }
};

